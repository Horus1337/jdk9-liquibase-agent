wrapper {
    gradleVersion = '4.6'
    distributionType = org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL
}


allprojects {

    repositories {
        jcenter()
    }

    apply plugin: 'java'

    compileJava {
        inputs.property("moduleName", 'eu.mhavlik.agent.jdk9liquibase')
        doFirst {
            options.compilerArgs = ['--module-path', classpath.asPath]
            classpath = files()
        }
    }

    jar.doFirst {
        manifest {
            def manifestCp = configurations.runtime.files.collect {
                "${it.name}"
            }.join(' ')
            attributes(
                    "Created-By": "Michael Havlik",
                    "Premain-Class": "eu.mhavlik.agent.LiquibasePatcher",
                    "Boot-Module-Path": manifestCp,
                    "Can-Redefine-Classes": "true",
                    "Can-Retransform-Classes": "true",
                    "Implementation-Version": "1.0"
            )
        }
    }

    dependencies {
        compile group: 'net.bytebuddy', name: 'byte-buddy', version: '1.7.6'
    }
}